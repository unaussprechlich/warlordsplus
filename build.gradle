buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = targetCompatibility = "1.8"
compileJava.options.encoding = 'UTF-8'

version = project_version
group= "net.unaussprechlich" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "$archive_name-$mc_version"

minecraft {
    version = "$mc_version-$forge_version-$mc_version"
    runDir = "run"
    mappings = "stable_22"
    clientJvmArgs = ["-XX:-DisableExplicitGC"]
    makeObfSourceJar = false
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')

    embed('org.spongepowered:mixin:0.8-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    embed "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    embed "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    embed ('org.slick2d:slick2d-core:1.0.2') {
        exclude module: 'lwjgl'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.WARN
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }


    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"

    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
    copy {
        from("src/main/resources")
        into("build/classes/java/main")
    }
}

jar {
    dependsOn configurations.compile

    manifest {
        attributes 'FMLCorePlugin': 'net.unaussprechlich.mixin.MixinLoadingPlugin'
        attributes 'FMLCorePluginContainsMod': 'true'
        attributes 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        attributes 'TweakOrder': 0
        attributes 'MixinConfigs': 'net/unaussprechlich/mixin/mixins_config.json'
    }

    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

idea { module { inheritOutputDirs = true; } }

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.3'
}